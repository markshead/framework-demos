apply plugin: "java"
apply plugin: "maven"
apply plugin: "idea"
apply plugin: "jetty"
apply plugin: "cargo"

group = "net.xeric.springmvc"
version = "1.0-SNAPSHOT"
description = "Spring MVC based JPA application"

project.ext.versions = [
        spring: "3.1.2.RELEASE",
        slf4j: "1.6.1",
        log4j: "1.2.16"
]

configurations {
    provided
}
sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    compile("org.springframework:spring-context:$versions.spring") {
        exclude module: "commons-logging"
    }

    compile "org.springframework:spring-webmvc:$versions.spring"
    compile "org.springframework:spring-web:$versions.spring"
    compile "org.springframework:spring-orm:$versions.spring"
    compile "org.springframework:spring-tx:$versions.spring"
    compile "org.slf4j:slf4j-api:$versions.slf4j"
    compile "javax.validation:validation-api:1.0.0.GA"
    compile "org.hibernate:hibernate-validator:4.3.0.Final"
    compile "org.hibernate:hibernate-core:4.1.4.Final"
    compile "org.hibernate:hibernate-entitymanager:4.1.4.Final"
    compile "cglib:cglib-nodep:2.2"

    runtime "org.slf4j:jcl-over-slf4j:$versions.slf4j"
    runtime "org.slf4j:slf4j-log4j12:$versions.slf4j"
    runtime "log4j:log4j:$versions.log4j"
    runtime "joda-time:joda-time:1.6.2"
    runtime "joda-time:joda-time-jsptags:1.0.2"
    runtime "com.h2database:h2:1.3.167"

    provided "javax.servlet.jsp:jsp-api:2.1"
    provided "javax.servlet:servlet-api:2.5"
    provided "javax.servlet:jstl:1.2"

    testCompile "junit:junit:4.7"
    def cargoVersion = '1.2.2'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion", "org.codehaus.cargo:cargo-ant:$cargoVersion"
}

repositories {
    mavenRepo url: "https://repository.jboss.org/nexus/content/repositories/releases"
    mavenCentral();
}

idea {
    project {
        jdkName = "1.6"
    }
    module {
        inheritOutputDirs = false
        outputDir = file("build/classes/main")
        testOutputDir = file('build/classes/test')

        downloadJavadoc = true
        downloadSources = true
    }
}

buildscript {
    repositories {
        add(new org.apache.ivy.plugins.resolver.URLResolver()) {
            name = 'GitHub'
            addArtifactPattern 'http://cloud.github.com/downloads/[organization]/[module]/[module]-[revision].[ext]'
        }
    }

    dependencies {
        classpath 'bmuschko:gradle-cargo-plugin:0.5.4'
    }
}

cargo {
    containerId = 'tomcat6x'
    port = 80
    deployable {
        context = 'springmvc'
    }

    remote {
        hostname = ratingsDeployHostname
        username = ratingsDeployUserName
        password = ratingsDeployPassword
    }

}

task writePom << {
    pom {
        project {

            build {
                plugins {

                    plugin {
                        groupId = "org.mortbay.jetty"
                        artifactId = "maven-jetty-plugin"
                        version = "6.1.9"
                    }

                }
            }

        }
    }.writeTo("pom.xml")
}

